apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId 'slowscript.warpinator'
        minSdkVersion 21 //Required by NSD (attributes)
        targetSdkVersion 29
        versionCode 1021
        versionName "1.2.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'org.openjax.security:nacl:0.3.2' //Cannot update - cannot resolve
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.65' //Cannot update - class version 59
    implementation 'io.grpc:grpc-netty:1.32.1'
    implementation 'io.grpc:grpc-okhttp:1.32.1'
    implementation ('io.grpc:grpc-protobuf:1.32.1') {
        exclude group: 'com.google.api.grpc', module: 'proto-google-common-protos'
    }
    implementation 'io.grpc:grpc-stub:1.32.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.conscrypt:conscrypt-android:2.5.0'

    implementation 'com.github.tony19:logback-android:2.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.guava:guava:29.0-android' //This was included by gRPC anyway, so why not use it
    implementation 'org.jmdns:jmdns:3.5.6'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.31.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {  }
            }
            task.plugins {
                grpc { }
            }
        }
    }
}

//If there is a better way to get rid of Netty logging, let me know
configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('ch.qos.logback:logback-classic') with module('com.github.tony19:logback-android:2.0.0')
        }
    }
}
