apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdk 34

    defaultConfig {
        applicationId 'slowscript.warpinator'
        minSdkVersion 21 //Required by NSD (attributes)
        targetSdkVersion 34
        versionCode 1071
        versionName "1.7.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/io.netty.versions.properties']
        }
    }
    namespace 'slowscript.warpinator'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2' //1.4 needs SDK 35
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'org.openjax.security:nacl:0.3.2' //Update available, but API is weird now
    implementation 'org.bouncycastle:bcpkix-jdk14:1.80'
    implementation 'io.grpc:grpc-netty:1.49.0' //Updating gRPC caused a problem with connecting, must investigate
    implementation 'io.grpc:grpc-okhttp:1.49.0'
    implementation ('io.grpc:grpc-protobuf:1.49.0') {
        exclude group: 'com.google.api.grpc', module: 'proto-google-common-protos'
    }
    implementation 'io.grpc:grpc-stub:1.49.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.conscrypt:conscrypt-android:2.5.3'

    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.guava:guava:33.4.0-android' //This was included by gRPC anyway, so why not use it
    implementation 'org.jmdns:jmdns:3.6.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'com.google.zxing:core:3.5.3'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.5'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.57.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {  }
            }
            task.plugins {
                grpc { }
            }
        }
    }
}

//If there is a better way to get rid of Netty logging, let me know
configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('ch.qos.logback:logback-classic') using module('com.github.tony19:logback-android:3.0.0')
        }
    }
}
