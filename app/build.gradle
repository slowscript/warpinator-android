apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 36

    defaultConfig {
        applicationId 'slowscript.warpinator'
        minSdkVersion 21 //Required by NSD (attributes)
        targetSdkVersion 35
        versionCode 1084
        versionName "1.8.4"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/io.netty.versions.properties']
        }
    }
    namespace 'slowscript.warpinator'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core:1.17.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.documentfile:documentfile:1.1.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'org.openjax.security:nacl:0.3.2' //Update available, but API is weird now
    implementation 'org.bouncycastle:bcpkix-jdk15to18:1.82'
    implementation 'io.grpc:grpc-netty:1.75.0'
    implementation 'io.grpc:grpc-okhttp:1.75.0'
    implementation ('io.grpc:grpc-protobuf:1.75.0') {
        exclude group: 'com.google.api.grpc', module: 'proto-google-common-protos'
    }
    implementation 'io.grpc:grpc-stub:1.75.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.conscrypt:conscrypt-android:2.5.3'

    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.guava:guava:33.4.8-android' //This was included by gRPC anyway, so why not use it
    implementation 'org.jmdns:jmdns:3.5.8' //Device discovery worsened after update, let's see if this was the problem
                                           // Also, new versions require desugaring on Android 5 and 6
    implementation 'org.slf4j:slf4j-api:2.0.17' // For jmdns, it declares a too old dependency
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'com.google.zxing:core:3.5.3'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.8'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.75.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {  }
            }
            task.plugins {
                grpc { }
            }
        }
    }
}

//If there is a better way to get rid of Netty logging, let me know
configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('ch.qos.logback:logback-classic') using module('com.github.tony19:logback-android:3.0.0')
        }
    }
}
